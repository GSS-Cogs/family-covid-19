PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wg: <http://gss-data.org.uk/data/gss_data/covid-19/wg-notifications-of-deaths-of-residents-related-to-covid-19-in-adult-care-homes#dimension/>
PREFIX wg-cod: <http://gss-data.org.uk/data/gss_data/covid-19/wg-notifications-of-deaths-of-residents-related-to-covid-19-in-adult-care-homes#concept/cause-of-death/>
PREFIX ons: <http://gss-data.org.uk/data/gss_data/covid-19/ons-number-of-deaths-in-care-homes-notified-to-the-care-quality-commission-england#dimension/>
PREFIX ons-cod: <http://gss-data.org.uk/data/gss_data/covid-19/ons-number-of-deaths-in-care-homes-notified-to-the-care-quality-commission-england#concept/cause-of-death/>
PREFIX meas: <http://gss-data.org.uk/def/measure/>
PREFIX stat: <http://statistics.data.gov.uk/def/statistical-entity#>
PREFIX sdmx: <http://purl.org/linked-data/sdmx#>
PREFIX sdmxd: <http://purl.org/linked-data/sdmx/2009/dimension#>
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX scovo: <http://purl.org/NET/scovo#>
PREFIX xsd:    <http://www.w3.org/2001/XMLSchema#>

SELECT ?geocode ?date ?deaths
WHERE {
  {
    ?obs wg:cause-of-death wg-cod:total ;
         meas:count ?count ;
         wg:notification-date ?date_uri ;
         wg:area-code ?code .
  } UNION {
            ?obs ons:cause-of-death ons-cod:involving-covid-19 ;
                 meas:cumulative-count ?count ;
                 sdmxd:refPeriod ?date_uri ;
                 ons:local-authority ?code
          }
  ?code rdfs:label ?geocode .
  ?date_uri scovo:min ?start .
  ?date_uri scovo:max ?end .
  BIND( ?count / (DAY(?end - ?start) + 1) as ?deaths)
  BIND( xsd:date(?end) as ?date )
}

ORDER BY ?geocode ?date